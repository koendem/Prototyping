// Name:ExprParser.jjt
// Date: 29 June 2016
//
// Definition of the ExprParser syntax. Includes abstract syntax tree generation
//

options
{
  LOOKAHEAD=2;
  IGNORE_CASE = false;
  MULTI=true;
  VISITOR = true;
  STATIC=false;
  NODE_CLASS="com.silvermoongroup.navexpressionparser.NavExpressionSimpleNode";
}

PARSER_BEGIN(ExprParser)
package com.silvermoongroup.navexpressionparser;

import java.io.*;
import java.util.*;


public class ExprParser
{



}

PARSER_END(ExprParser)

TOKEN_MGR_DECLS:
{

}

SKIP:   /* Whitespace */
{
  "\t"
| "\n"
| "\r"

}

TOKEN:
{
  <CONCAT: ".">
| <LITERAL: "\"" (["a"-"z","A"-"Z","0"-"9", " ", "<", ">", "&","/","'",".","-"])+ "\"" >
| <TOPLEVELAGREEMENT: "topLevelAgreement" | "agreement">
| <SELF: "this" | "self">
| <REQUEST: "request">
| <CHILDREN: "children">
| <ANCESTOR: "ancestor">
| <DESCENDANTS: "descendants">
| <INSTANCEOF: "instanceOf">
| <PARENT: "parent">
| <PRODUCT: "product">
| <ALL: "all">
| <KIND: "kind">
| <CONTAINS: "contains">
| <FIRST: "first">
| <ISEMPTY: "isEmpty">
| <SIZE: "size">
| <UNIQUE: "unique">
| <EXPRESSIONSTEP: ((["a"-"z","A"-"Z","0"-"9"])+  | ("\'" (["a"-"z","A"-"Z","0"-"9"," "])+  "\'")
             ) >
| <SEMIC: ";">
| <LPAREN: "(">
| <RPAREN: ")">
}


/* production rules for the language.
navPathExpression =
              <LITERAL>
			  | <PRODUCT>
              | <TOPLEVELAGREEMENT> [<CONCAT> compoundNavigation]
              | <SELF> [<CONCAT> compoundNavigation]
              | <REQUEST> [<CONCAT> compoundNavigation]
              | <PARENT> [<CONCAT> compoundNavigation]
              | compoundNavigation


compoundNavigation = kindNavigation [ <SEMIC> domainNavigation ]


kindNavigation = navigationStep() [ <CONCAT> kindNavigation ]

navigationStep = <KINDSTEP>

type = <EXPRESSIONSTEP>

doaminNavigation = navigationStep() [ <CONCAT> domainNavigation ]


ancestor = <ANCESTOR> <LPAREN> type() <RPAREN>

children = <CHILDREN> <LPAREN> type() <RPAREN>

descendants = <DESCENDANTS> <LPAREN> type() <RPAREN>

instanceOf = <INSTANCEOF> <LPAREN> type() <RPAREN>


Collection processing:

processing = contains() | first() | isEmpty() | size() | unique()
contains = <CONTAINS> <LPAREN> type() <RPAREN>

*/

SimpleNode navPathExpression() : {}
{
 ((    (literal())
     | (product())
     | ( topLevelAgreement() [ <CONCAT> compoundNavigation()])
     | ( self() [ <CONCAT> compoundNavigation()])
     | ( request() [ <CONCAT> compoundNavigation()])
     | ( parent() [ <CONCAT> compoundNavigation()])
     | (compoundNavigation())
 ) (<CONCAT> processing())? )
 {return jjtThis;}
}

void product() : {Token t;}
{
 t= <PRODUCT> {jjtThis.value = t.image;}
}

void topLevelAgreement() : {Token t;}
{
 t= <TOPLEVELAGREEMENT> {jjtThis.value = t.image;}
}

void request() : {Token t;}
{
 t= <REQUEST> {jjtThis.value = t.image;}
}

void self() : {Token t;}
{
 t= <SELF> {jjtThis.value = t.image;}
}

void parent() : {Token t;}
{
 t= <PARENT> {jjtThis.value = t.image;}
}

void literal() : { Token t; }
{
    t = <LITERAL> {jjtThis.value = t.image; }
}

void compoundNavigation() #void : {Token t;}
{
  (kindNavigation()) [ (domainNavigation()) ]
}

void all() : {Token t;}
{
 (t = <ALL> {jjtThis.value = t.image;} )
}

void kind() : {Token t;}
{
 (t = <KIND> {jjtThis.value = t.image;} )
}

void children() : {}
{
  <CHILDREN> <LPAREN> type() <RPAREN>
}

void ancestor() : {}
{
  <ANCESTOR> <LPAREN> type() <RPAREN>
}

void descendants() : {}
{
  <DESCENDANTS> <LPAREN> type() <RPAREN>
}
void instanceOf() : {}
{
  <INSTANCEOF> <LPAREN> type() <RPAREN>
}
void kindNavigation() #void : {}
{
    (navigationStep()
    | all()
	| kind()
    | children()
	| ancestor()
    | descendants()
	| instanceOf()
    )  [ <CONCAT> kindNavigation() ]
}

void navigationStep() : {Token t; }
{
  t = <EXPRESSIONSTEP> {jjtThis.value = t.image; }
}

void type() : {Token t; }
{
  t = <EXPRESSIONSTEP> {jjtThis.value = t.image; }
}

void domainNavigation() : {}
{
    <SEMIC> domainNavigations()
}

void domainNavigations() #void : {}
{
    (kindNavigation())  [ <CONCAT> domainNavigations() ]
}

//collection processing
//could add #void, but leaving it out might be easier for the interpreter to classify
void processing() : {}
{
	(contains() | first() | isEmpty() | size() | unique())
}
void contains() : {}
{
  <CONTAINS> <LPAREN> type() <RPAREN>
}

void first() : {Token t;}
{
 (t = <FIRST> {jjtThis.value = t.image;} )
}

void isEmpty() : {Token t;}
{
 (t = <ISEMPTY> {jjtThis.value = t.image;} )
}

void size() : {Token t;}
{
 (t = <SIZE> {jjtThis.value = t.image;} )
}

void unique() : {Token t;}
{
 (t = <UNIQUE> {jjtThis.value = t.image;} )
}



